Package:
    'package' name=ID 'version' '=' version=PackageVersion

   items*=PackageItem

;

PackageItem:
    TypeDef|Interface
;


PackageVersion:
    INT ('.' INT)*
;


TypeDef:
    'typedef' name=ID '['
        fields+=TypeField
    ']'
;

TypeField:
    (id=INT ':')? type=DataType name=ID
;

Interface:
    'interface' name=ID '['
        functions+=Function
    ']'
;

Function:
    'def' name=ID '(' params*=FunctionParameter[','] ')'
;

FunctionParameter:
    type=DataType (p_modifier=ParameterModifier)? name=ID ('='default=DefaultValue)?
;


DefaultValue:
    'None' | STRING | NUMBER
;

ParameterModifier:
    '**' | '*'
;

DataType:
    Number | Collection | CustomType
;

CustomType:
    type=[TypeDef]
;

Number:
    Integer | Float
;

Integer:
    'i16' | 'i32' | 'i64'
;

Float:
    'float' | 'double'
;

Collection:
    Sequence | Set | Dict
;

Sequence:
    String | List
;

String:
    'str'
;

List:
    TypedList | NonTypedList
;

TypedList:
    'list' '<' type=DataType '>' ('[' len=INT ']')?
;

NonTypedList:
    'list'
;

Set:
    TypedSet | NonTypedSet
;

TypedSet:
    'set' '<' type=DataType '>'
;

NonTypedSet:
    'set'
;

Dict:
    TypedDict | NonTypedDict
;

TypedDict:
    'dict' '<' key_type=DataType ',' value_type=DataType '>'
;

NonTypedDict:
    'dict'
;